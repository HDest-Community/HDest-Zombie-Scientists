class ZombieScientistSpawner : IdleDummy {
    
    int maxSpawns;
    int numSpawned;

    override void PostBeginPlay() {
        super.PostBeginPlay();

        angle = random(1, 360);
        
        let i = random();
        for (let j = 8; j >= 0; j--) {
            if (i >> j) {
                maxSpawns = 8 - j;
                break;
            }
        }
    }

    action void A_SpawnScientists(int dist) {

        while (invoker.numSpawned < invoker.maxSpawns) {
            let spawnAngle = (invoker.numSpawned % invoker.maxSpawns) * (360 / invoker.maxSpawns) + angle;

            let className = '';

            let i = random(1, 660);
            if (i >= 560) className = 'Hideous_ZombieLadyKnife';
            else if (i >= 460) className = 'Hideous_ZombieAxe';
            else if (i >= 360) className = 'Hideous_ZombieCrowbar';
            else if (i >= 290) className = 'Hideous_ZombieLadyCleaver';
            else if (i >= 230) className = 'Hideous_ZombieLadyWrench';
            else if (i >= 140) className = 'Hideous_ZombieHammer';
            else if (i >= 60) className = 'Hideous_ZombieLadySyringe';
            else className = 'Hideous_ZombieChainsaw';
            
            if (hd_debug) console.printf("Spawning "..className.." #"..(invoker.numSpawned + 1).." of "..invoker.maxSpawns.." with angle="..spawnAngle.." & dist="..dist);

            let offs = AngleToVector(spawnAngle, dist);
            Spawn(className, pos + offs);
            invoker.numSpawned++;
        }
    }

    states {
        spawn:
            TNT1 A 0 nodelay A_SpawnScientists(maxSpawns > 1 ? 35 : 0);
            stop;
    }
}

//Already Dead Scientists for frag-related funtimes
class Dead_HumanVictim : RandomSpawner {
    default {
        Dropitem "Dead_ZombieLadyKnife",256,80;
        DropItem "Dead_ZombieLadyWrench",256,80;
        DropItem "Dead_ZombieLadyCleaver",256,80;
        DropItem "Dead_ZombieLadySyringe",256,80;
        DropItem "Dead_ZombieAxe",256,100;
        DropItem "Dead_ZombieCrowbar",256,100;
        DropItem "Dead_ZombieHammer",256,100;
        DropItem "Dead_ZombieChainsaw",256,100;
    }
}
