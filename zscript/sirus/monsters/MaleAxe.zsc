//Scientist w/ Axe
//One of the much slower civvie zombies all around, but with the second most
//damage dealt per blow. Even with blue armor, you'll feel it when he slams the
//axe head onto your noggin. Don't let him get close.

class Hideous_ZombieAxe : Hideous_ZombieScientist_Male
{
	default
	{
		//$Category "Monsters/Hideous Destructor"
		//$Title "Possessed Scientist(Axe)"
		//$Sprite "SCZAA1"

		health 120;
		gibhealth 130;

		HitObituary "$OB_ZOMBIESCIENTIST_FIREAXE_HIT";
		Obituary "$OB_ZOMBIESCIENTIST_FIREAXE";

		Hideous_ZombieScientist.mindamage 16;
		Hideous_ZombieScientist.maxdamage 30;

		DropItem "HDFireAxeDropper";
	}

	states
	{
		spawn:
			SCZA A 0;
			goto spawn2;
		melee:
			#### E 3 A_FaceTarget();
			#### F 5 A_FaceTarget();
			#### F 0 A_CustomMeleeAttack(random(mindamage, maxdamage) * 2, meleesound, "skeleton/swing", "teeth", true);
			#### F 0 A_CheckLatch();
			#### F 6;
			#### G 6;
			#### GGGG 0 A_CustomMeleeAttack(random(mindamage, maxdamage), meleesound, "skeleton/swing", "teeth", true);
			#### G 0 A_CheckEndMelee();
			#### G 0 A_Jump(256, "melee");
	}
}

class Dead_ZombieAxe : Hideous_ZombieAxe
{
	override void postbeginplay(){
		super.postbeginplay();
		A_Die("spawndead");
	}
}

class HDFireAxeDropper : Actor
{

	states
	{
		spawn:
			TNT1 A 0;
			TNT1 A 1 {
				string HDFireAxeName = "HDFireAxe";
				class<Actor> cls = HDFireAxeName;
				if (cls)
				{
					A_SpawnItemEX(cls,0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
				}
			}
			stop;
	}
}