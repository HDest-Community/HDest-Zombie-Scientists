//Scientist with Hammer
//Easily the deadliest of the victim scientists. He moves slower, but you'd be
//surprised how little a space helmet protects your head from a ten-ton steel
//hammer colliding with your head. Additionally he has a high amount of health
//and resilience due to a zerk drip, same as the Dragon Guard.

class Hideous_ZombieHammer : Hideous_ZombieScientist_Male
{
	default
	{
		//$Category "Monsters/Hideous Destructor"
		//$Title "Possessed Scientist(Hammer)"
		//$Sprite "SCZHA1"

		speed 17;

		HitObituary "$OB_ZOMBIESCIENTIST_HAMMER_HIT";
		Obituary "$OB_ZOMBIESCIENTIST_HAMMER";

		Hideous_ZombieScientist.mindamage 15;
		Hideous_ZombieScientist.maxdamage 50;

		DropItem "HDSledgehammerDropper";
	}

	states
	{
		spawn:
			SCZH A 0;
			goto spawn2;
		melee:
			#### E 6 A_FaceTarget();
			#### F 4 A_FaceTarget();
			#### F 0{
				A_CustomMeleeAttack(random(mindamage, maxdamage) * 2, meleesound, "skeleton/swing", "teeth", true);
				if(
					(target && distance3d(target) < latchrange)
					&&(random(0, 2))
				){
					setstatelabel("latch");
				}
			}
			#### F 10;
			#### G 9;
			#### GGGG 0 A_CustomMeleeAttack(random(mindamage, maxdamage), meleesound, "skeleton/swing", "teeth", true);
			#### F 10;
			#### G 8;
			#### F 0{
				A_CustomMeleeAttack(random(mindamage, maxdamage) * 2, meleesound, "skeleton/swing", "teeth", true);
				if(
					(target && distance3d(target) < latchrange)
					&&(random(0, 2))
				){
					setstatelabel("latch");
				}
			}
			#### G 0 {
				if(
					(target && distance3d(target) > meleerange)
				){
					setstatelabel("seerunnin");
				}
			}
			#### G 0 A_Jump(30, "meleeend");
			#### G 0 A_Jump(256, "melee");
	}
}

class HDSledgehammerDropper:Actor
{
	states
	{
		spawn:
			TNT1 A 0;
			TNT1 A 1 {
				string HDSledgehammerName = "HDSledgehammer";
				class<Actor> cls = HDSledgehammerName;
				if (cls)
				{
					A_SpawnItemEX(cls,0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
				}
			}
			stop;
	}
}