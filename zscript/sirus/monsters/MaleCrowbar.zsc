//Scientist w/ Crowbar
//Best described as "Axe scientist but shittier."

class Hideous_ZombieCrowbar : Hideous_ZombieScientist_Male
{
	default
	{
		//$Category "Monsters/Hideous Destructor"
		//$Title "Possessed Scientist(Crowbar)"
		//$Sprite "SCZCA1"

		speed 20;
		
		HitObituary "$OB_ZOMBIESCIENTIST_CROWBAR_HIT";
		Obituary "$OB_ZOMBIESCIENTIST_CROWBAR";

		Hideous_ZombieScientist.mindamage 10;
		Hideous_ZombieScientist.maxdamage 18;
		
		DropItem "HDCrowbarDropper";
	}

	states
	{
		spawn:
			SCZC A 0;
			goto spawn2;
		melee:
			#### E 6 A_FaceTarget();
			#### F 5 A_FaceTarget();
			#### F 0{
				A_CustomMeleeAttack(random(mindamage, maxdamage) * 2, meleesound, "skeleton/swing", "piercing", true);
				if(
					(target && distance3d(target) < latchrange)
					&&(random(0, 2))
				){
					setstatelabel("latch");
				}
			}
			#### F 4;
			#### G 7;
			#### GGGG 0 A_CustomMeleeAttack(random(mindamage, maxdamage), meleesound, "skeleton/swing", "teeth", true);
			#### G 0 {
				if(
					(target && distance3d(target) > meleerange)
				){
					setstatelabel("seerunnin");
				}
			}
			#### G 0 A_Jump(30, "meleeend");
			#### F 4;
			#### G 7;
			#### GGGG 0 A_CustomMeleeAttack(random(mindamage, maxdamage), meleesound, "skeleton/swing", "teeth", true);
			#### G 0 {
				if(
					(target && distance3d(target) > meleerange)
				){
					setstatelabel("seerunnin");
				}
			}
			#### G 0 A_Jump(30, "meleeend");
			#### G 0 A_Jump(256, "melee");
	}
}

class HDCrowbarDropper:Actor
{

	states
	{
		spawn:
			TNT1 A 0;
			TNT1 A 1 {
				string HDCrowbarName = "NHDACrowbar";
				class<Actor> cls = HDCrowbarName;
				if (cls)
				{
					A_SpawnItemEX(cls,0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
				}
			}
			stop;
	}
}