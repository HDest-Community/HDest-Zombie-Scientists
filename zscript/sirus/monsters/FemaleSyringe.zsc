//Scientist with Syringe
//The most unassuming but also the most dangerous. Their attack does very little
//raw damage, but sometimes it will give you a poisonous compound that suddenly causes you to drop to the ground, leaving you open to
//attacks. In addition they make very little noise, making it much easier for you to get snuck up on from behind.

class Hideous_ZombieLadySyringe : Hideous_ZombieScientist_Female
{
	action void BoopWithDrugs(int stunvalue, int fatiguevalue) {
		// I'm using a bool here to make the conditional more clear
		// Doing it this way is optional but easier to read
		bool validToStun = (
			target &&
			target is "HDPlayerPawn" &&
			target.health > 1 &&
			Distance2D (target) < (target.radius + radius) + 24
		);

		if (validToStun) {
			hdplayerpawn tat = HDPlayerPawn(target);
			tat.stunned += stunvalue;
			tat.fatigue += fatiguevalue;
			if (tat.incapacitated == 0)
			{
				if (hd_debug) console.printf("benis");
				A_StartSound("Derp/repair2",CHAN_BODY);
				SetStateLabel("InjectedWait");
			}
		}
	}

	default
	{
		//$Category "Monsters/Hideous Destructor"
		//$Title "Possessed Scientist(Poison)"
		//$Sprite "FSZSA1"

		speed 14;

		Hideous_ZombieScientist.mindamage 2;
		Hideous_ZombieScientist.maxdamage 5;

		SeeSound "";
		ActiveSound "";
		MeleeSound "weapons/chunksplat";

		HitObituary "$OB_ZOMBIESCIENTIST_SYRINGE_HIT";
		Obituary "$OB_ZOMBIESCIENTIST_SYRINGE";

		DropItem "PortableStimpack", 1;
	}

	states
	{
		spawn:
			FSZS A 0;
			goto spawn2;
		melee:
			#### E 3 A_FaceTarget();
			#### F 4 A_FaceTarget();
			#### F 0{
				A_CustomMeleeAttack(random(mindamage, maxdamage) * 2, meleesound, "skeleton/swing", "piercing", true);
				if(
					(target && distance3d(target) < latchrange)
					&&(random(0, 2))
				){
					setstatelabel("latch");
				}
			}
			#### F 0 { BoopWithDrugs(80, 20); }
			#### F 3;
			#### G 1;
			#### GGGG 0 A_CustomMeleeAttack(random(mindamage, maxdamage), meleesound, "skeleton/swing", "piercing", true);
			#### G 0 {
					if(
					(target && distance3d(target) < latchrange)
					&&(random(0,2))
				){
					setstatelabel("latch");
				}
			}
			#### G 0 {
				if(
					(target && distance3d(target) > meleerange)
				){
					setstatelabel("seerunnin");
				}
			}
			#### G 0 A_Jump(30, "meleeend");
			#### F 0 { BoopWithDrugs(80, 20); }
			#### F 3;
			#### G 1;
			#### GGGG 0 A_CustomMeleeAttack(random(mindamage, maxdamage), meleesound, "skeleton/swing", "piercing", true);
			#### G 0 {
				if(
					(target && distance3d(target) < latchrange)
					&&(random(0, 2))
				){
					setstatelabel("latch");
				}
			}
			#### G 0 A_Jump(30, "meleeend");
			#### G 0 {
				if(
					(target && distance3d(target) > meleerange)
				){
					setstatelabel("seerunnin");
				}
			}
			#### F 0 { BoopWithDrugs(80, 20); }
			#### F 3;
			#### G 1;
			#### GGGG 0 A_CustomMeleeAttack(random(mindamage, maxdamage), meleesound, "skeleton/swing", "piercing", true);
			#### G 0 {
				if(
					(target && distance3d(target) < meleerange)
					&&(random(0, 2))
				){
					setstatelabel("latch");
				}
			}
			#### G 0 A_Jump(30, "meleeend");
			#### G 0 {
				if(
					(target && distance3d(target) > meleerange)
				){
					setstatelabel("seerunnin");
				}
			}
			#### F 0 { BoopWithDrugs(80,20);}
			#### F 9;
			#### G 1;
			#### GGGG 0 A_CustomMeleeAttack(random(mindamage, maxdamage), meleesound, "skeleton/swing", "piercing", true);
			#### G 0 {
				if(
					(target && distance3d(target) < latchrange)
					&&(random(0, 2))
				){
					setstatelabel("latch");
				}
			}
			#### G 0 {
				if(
					(target && distance3d(target) > meleerange)
				){
					setstatelabel("seerunnin");
				}
			}
			#### G 0 A_Jump(30, "meleeend");
			#### G 0 A_Jump(256, "melee");
		InjectedWait:
			#### A 4;
			#### B 5;
			#### C 5;
			#### D 3;
			#### D 0 {
				bool validToStun = (
					target &&
					target is "HDPlayerPawn" &&
					target.health > 1 &&
					Distance2D (target) < (target.radius + radius) + 24
				);

				if (validToStun) {
					hdplayerpawn tat = HDPlayerPawn(target);
				
					if(tat) {
						if (hd_debug) console.printf("yeet");
						tat.incapacitated = 1;
						tat.incaptimer = 140;
					}
				}	
			}
			goto see;
	}
}