//Scientist with Hammer
//Easily the deadliest of the victim scientists. He moves slower, but you'd be
//surprised how little a space helmet protects your head from a ten-ton steel
//hammer colliding with your head. Additionally he has a high amount of health
//and resilience due to a zerk drip, same as the Dragon Guard.

class Hideous_ZombieHammer : Hideous_ZombieScientist_Male
{
	default
	{
		//$Category "Monsters/Hideous Destructor"
		//$Title "Possessed Scientist(Hammer)"
		//$Sprite "SCZHA1"

		speed 12;

		tag "$TAG_ZOMBIESCIENTIST_HAMMER";
		HitObituary "$OB_ZOMBIESCIENTIST_HAMMER_HIT";
		Obituary "$OB_ZOMBIESCIENTIST_HAMMER";

		Hideous_ZombieScientist.mindamage 15;
		Hideous_ZombieScientist.maxdamage 50;

		DropItem "HDSledgehammerDropper";
	}

	states
	{
		spawn:
			SCZH A 0;
			goto spawn2;
		melee:
			#### BE 5 A_FaceTarget();
			#### F 15 A_CustomMeleeAttack(random(mindamage, maxdamage) * 2, meleesound, misssound, "bashing", true);
			#### F 0 A_CheckLatch();
			#### EBE 5;
			#### F 15 A_CustomMeleeAttack(random(mindamage, maxdamage) * 2, meleesound, misssound, "bashing", true);
			#### EBE 6;
			#### F 15 A_CustomMeleeAttack(random(mindamage, maxdamage), meleesound, misssound, "bashing", true);
			#### F 0 A_CheckLatch();
			#### G 0 A_CheckEndMelee();
			#### G 0 A_Jump(256, "melee");
	}
}

class Dead_ZombieHammer : Hideous_ZombieHammer
{
	override void postbeginplay(){
		super.postbeginplay();
		A_Die("spawndead");
	}
}

class HDSledgehammerDropper : Actor
{
	states
	{
		spawn:
			TNT1 A 0;
			TNT1 A 1 {
				string HDSledgehammerName = "HDSledgehammer";
				class<Actor> cls = HDSledgehammerName;
				if (cls)
				{
					A_SpawnItemEX(cls,0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
				}
			}
			stop;
	}
}
