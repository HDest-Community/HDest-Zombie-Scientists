//Scientist w/ Chainsaw
//Best described as "Axe scientist but better."

class Hideous_ZombieChainsaw : Hideous_ZombieScientist {
    mixin MaleScientist;

    int sawTicker;
    
    Sound sawIdle;   property sawIdle:sawIdle;
    Sound misssound; property misssound:misssound;

    override void Tick() {
        super.Tick();

        if (health > 0 && !isFrozen()) {
            if (sawTicker > 4) {
                A_StartSound(sawIdle);
                sawTicker = 0;
            } else {
                sawTicker++;
            }
        }
    }

    action void A_WeaponAttack(int damageMulti, string damageType, float latchChance = 0.333, float comboChance = 0.333) {
        A_CustomMeleeAttack(random(invoker.minDamage, invoker.maxDamage) * damageMulti, invoker.meleesound, invoker.misssound, damageType);
        A_CheckLatch(latchChance);
        A_CheckEndMelee(comboChance);
    }

    default {
        //$Category "Monsters/Hideous Destructor"
        //$Title "Possessed Scientist(Chainsaw)"
        //$Sprite "SCZCS1"

        speed 10;
        
        HitObituary "$OB_ZOMBIESCIENTIST_CHAINSAW_HIT";
        Obituary "$OB_ZOMBIESCIENTIST_CHAINSAW";
        
        meleesound "enemy/zombiesci/male/sawhit";

        Hideous_ZombieScientist.mindamage 20;
        Hideous_ZombieScientist.maxdamage 50;

        Hideous_ZombieChainsaw.misssound "enemy/zombiesci/male/sawmiss";
        Hideous_ZombieChainsaw.sawIdle "enemy/zombiesci/male/sawidle";
        
        Hideous_ZombieScientist.dropName "Lumberjack";
    }

    states {
        spawn:
            SCZS A 0;
            goto spawn2;
        melee:
            #### E random(15, 25) A_FaceLastTargetPos();
            #### FFFF 5 A_WeaponAttack(random(1, 2), "slashing", 0.5, 0.1);
            #### FB random(10, 20);
            loop;
        meleeend:
            #### FB random(10, 20) A_Recoil(-frandom(0, 1));
            goto see;
        latch:
            #### F random(2, 4);
            #### G random(2, 4) A_Latch(2, "slashing");
            #### F random(2, 4);
            loop;
    }
}

class Dead_ZombieChainsaw : Hideous_ZombieChainsaw {
    override void postbeginplay() {
        super.postbeginplay();
        A_Die("spawndead");
    }
}