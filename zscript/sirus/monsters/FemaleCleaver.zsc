//Scientist Lady with Cleaver
//The rapid hack and slash nature of knife lady, now with MUCH more damage, and
//slightly more speed to boot. Idk, blame zerk. Big danger, avoid getting slashed
//if you can.

class Hideous_ZombieLadyCleaver : Hideous_ZombieLadyKnife
{
	default
	{
		//$Category "Monsters/Hideous Destructor"
			//$Title "Possessed Scientist(Cleaver)"
			//$Sprite "FSZCA1"
		health 100;
		gibhealth 130;
		scale 1.0;
		HitObituary "%o was taken to the chop-shop.";
		Obituary "%o was taken to the chop-shop.";
		DropItem "PortableStimpack", 1;
	}
	
	states
	{
		spawn:
		spawn2:
			FSZC A 0 A_JumpIf(!bambush,"spawnwander");
			FSZC A 0 A_Jump(256,"spawnstill");
		spawnwander:
			FSZC A 1 A_HDLook();
			FSZC ABCD 8 A_Wander(CHF_DONTTURN);
			FSZC A 0 A_Jump(48,"spawnstill");
			FSZC A 0 A_Jump(48,1);
			loop;
			FSZC A 0 A_StartSound("enemy/zombiescifemale/active",CHAN_VOICE);
		spawnstill:
			FSZC D 5 A_Jump(48,"spawnwander");
			FSZC A 0 A_HDLook();
			FSZC E 5 A_SetAngle(angle+random(-20,20));
			FSZC EE 5 A_HDLook();
			loop;
		melee:
			"####" E 3 A_FaceTarget();
			"####" F 5 A_FaceTarget();
			"####" F 0{
				A_CustomMeleeAttack(random(9,27)*2,"enemy/zombiescifemale/slash","skeleton/swing","teeth",true);
				if(
					(target&&distance3d(target)<40)
					&&(random(0,2))
				){
					setstatelabel("latch");
				}
			}
			"####" F 4;
			"####" G 5;
			"####" GGGG 0 A_CustomMeleeAttack(random(9,27),"enemy/zombiescifemale/slash","skeleton/swing","teeth",true);
			"####" G 0 {
				if(
					(target&&distance3d(target)>40)
				){
					setstatelabel("seerunnin");
				}
			}
			"####" G 0 A_Jump(30,"meleeend");
			"####" F 4;
			"####" G 5;
			"####" GGGG 0 A_CustomMeleeAttack(random(9,27),"enemy/zombiescifemale/slash","skeleton/swing","teeth",true);
			"####" G 0 A_Jump(30,"meleeend");
			"####" G 0 {
				if(
					(target&&distance3d(target)>40)
				){
					setstatelabel("seerunnin");
				}
			}
			"####" F 4;
			"####" G 5;
			"####" GGGG 0 A_CustomMeleeAttack(random(9,27),"enemy/zombiescifemale/slash","skeleton/swing","teeth",true);
			"####" G 0 A_Jump(30,"meleeend");
			"####" G 0 {
				if(
					(target&&distance3d(target)>40)
				){
					setstatelabel("seerunnin");
				}
			}
			"####" F 9;
			"####" G 1;
			"####" GGGG 0 A_CustomMeleeAttack(random(9,27),"enemy/zombiescifemale/slash","skeleton/swing","teeth",true);
			"####" G 0 {
				if(
					(target&&distance3d(target)>40)
				){
					setstatelabel("seerunnin");
				}
			}
			"####" G 0 A_Jump(30,"meleeend");
			"####" G 0 A_Jump(256,"melee");
		meleeend:
			"####" E 2;
			"####" A 0 setstatelabel("see");
		ungib:
			"####" W 6{
				A_SpawnItemEx("MegaBloodSplatter",0,0,4,
					vel.x,vel.y,vel.z+3,0,
					SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM
				);
			}
			"####" VU 8;
			"####" TSR 6;
			"####" Q 0 A_SetScale(1.0);
			"####" QPO 4;
			goto checkraise;
		xdeath:
			"####" O 1 A_XScream();
			"####" O 0 A_NoBlocking();
			"####" OPQR 3 spawn("MegaBloodSplatter",pos+(0,0,34),ALLOW_REPLACE);
			"####" S 0 A_SetScale(0.6);
			"####" STUV 2;
			goto xdead;
		xxxdeath:
			"####" O 0 A_XScream();
			"####" O 4 A_NoBlocking();
			"####" PQRS 3;
			"####" S 0 A_SetScale(0.6);
			"####" TUV 3;
		xdead:
			"####" W 5 canraise{
				//if(abs(vel.z)<2)frame++;
			}loop;
	}
}