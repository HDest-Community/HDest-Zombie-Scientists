//Scientist with Syringe
//The most unassuming but also the most dangerous. Their attack does very little
//raw damage, but sometimes it will give you a poisonous compound that suddenly causes you to drop to the ground, leaving you open to
//attacks. In addition they make very little noise, making it much easier for you to get snuck up on from behind.

class ZombieLadySyringeDrug {
    name injectorName;
    int injectAmount;
    name drugTranslation;
    name pickupName;

    static ZombieLadySyringeDrug create(name pickupName, name injectorName, int injectAmount, name drugTranslation) {
        let drug = ZombieLadySyringeDrug(new('ZombieLadySyringeDrug'));

        if (drug) {
            drug.injectorName = injectorName;
            drug.injectAmount = injectAmount;
            drug.drugTranslation = drugTranslation;
            drug.pickupName = pickupName;
        }

        return drug;
    }
}

class Hideous_ZombieLadySyringe : Hideous_ZombieScientist {
    mixin FemaleScientist;

    ZombieLadySyringeDrug drug;

    override void PostBeginPlay() {
        super.PostBeginPlay();
        
        drug = SelectSyringeDrug();

        dropName = drug.pickupName;

        A_SetTranslation(drug.drugTranslation);
    }

    private ZombieLadySyringeDrug SelectSyringeDrug() {
        switch (random(0, 1)) {
            case 0:
                return ZombieLadySyringeDrug.create('PortableStimpack', 'InjectStimDummy', HDStim.HDSTIM_DOSE, 'ZScientist_Syringe_Stim');
            case 1:
                return ZombieLadySyringeDrug.create('PortableBerserkPack', 'InjectZerkDummy', HDZerk.HDZERK_MAX, 'ZScientist_Syringe_Zerk');
            default:
                A_Log('Unknown Syringe Drug!');
                break;
        }

        return null;
    }

    action void A_WeaponAttack(int damageMulti, string damageType, float latchChance) {
        invoker.A_FireballerScratch(null, random(invoker.minDamage, invoker.maxDamage) * damageMulti, ballchance: 0, damagetype: damageType);
        A_BoopWithDrugs(80, 20);
        A_CheckLatch(latchChance);
        A_CheckEndMelee();
    }

    action void A_BoopWithDrugs(int stunValue, int fatigueValue) {
        if (
            target is "HDPlayerPawn"
            && target.health > 1
            && Distance2D (target) < (target.radius + radius) + 24
        ) {
            hdplayerpawn tat = HDPlayerPawn(target);

            actor a = spawn(invoker.drug.injectorName, tat.pos, ALLOW_REPLACE);
            a.accuracy = 40;
            a.target = HDPlayerPawn(tat);

            A_StartSound(meleesound, CHAN_BODY);
            SetStateLabel("InjectedWait");
        }
    }

    action void A_UnBoopWithDrugs() {
        if (
            target is "HDPlayerPawn"
            && target.health > 1
            && Distance2D (target) < (target.radius + radius) + 24
        ) {
            hdplayerpawn tat = HDPlayerPawn(target);
        
            if (tat) {
                tat.incapacitated = 1;
                tat.incaptimer = 140;

                // Escape from player before they can retalliate
                HDMobAI.Frighten(tat, 512);
            }

            target = null;
        }    
    }

    default {
        //$Category "Monsters/Hideous Destructor"
        //$Title "Possessed Scientist(Poison)"
        //$Sprite "FSZSA1"

        speed 8;

        Hideous_ZombieScientist.mindamage 2;
        Hideous_ZombieScientist.maxdamage 5;

        SeeSound "";
        ActiveSound "";
        MeleeSound "weapons/chunksplat";

        tag "$TAG_ZOMBIESCIENTIST_SYRINGE";
        HitObituary "$OB_ZOMBIESCIENTIST_SYRINGE_HIT";
        Obituary "$OB_ZOMBIESCIENTIST_SYRINGE";
    }

    states {
        spawn:
            FSZS A 0;
            goto spawn2;
        melee:
            #### EEE random(2, 3) A_FaceLastTargetPos();
            #### F 5 A_WeaponAttack(1, "piercing", 0.0);
            #### FB random(4, 12);
            loop;
        meleeend:
            #### FB random(4, 12);
            goto see;
        InjectedWait:
            #### FEEB 4;
            #### B 0 A_UnBoopWithDrugs();
            goto see;
    }
}

class Dead_ZombieLadySyringe : Hideous_ZombieLadySyringe {
    override void postbeginplay() {
        super.postbeginplay();
        A_Die("spawndead");
    }
}
