//Scientist with Syringe
//The most unassuming but also the most dangerous. Their attack does very little
//raw damage, but sometimes it will give you a poisonous compound that suddenly causes you to drop to the ground, leaving you open to
//attacks. In addition they make very little noise, making it much easier for you to get snuck up on from behind.

class Hideous_ZombieLadySyringe : Hideous_ZombieScientist {
    mixin FemaleScientist;

    action void A_WeaponAttack(int damageMulti, string damageType) {
        A_CustomMeleeAttack(random(invoker.minDamage, invoker.maxDamage) * damageMulti, invoker.meleesound, invoker.misssound, damageType);
        A_BoopWithDrugs(80, 20);
        A_CheckLatch();
        A_CheckEndMelee();
    }

    action void A_BoopWithDrugs(int stunValue, int fatigueValue) {
        if (
            target is "HDPlayerPawn"
            && target.health > 1
            && Distance2D (target) < (target.radius + radius) + 24
        ) {
            hdplayerpawn tat = HDPlayerPawn(target);

            tat.stunned += stunValue;
            tat.fatigue += fatigueValue;

            if (!tat.incapacitated) {
                A_StartSound(meleesound, CHAN_BODY);
                SetStateLabel("InjectedWait");
            }
        }
    }

    action void A_UnBoopWithDrugs() {
        if (
            target is "HDPlayerPawn"
            && target.health > 1
            && Distance2D (target) < (target.radius + radius) + 24
        ) {
            hdplayerpawn tat = HDPlayerPawn(target);
        
            if(tat) {
                tat.incapacitated = 1;
                tat.incaptimer = 140;
            }
        }    
    }

    default {
        //$Category "Monsters/Hideous Destructor"
        //$Title "Possessed Scientist(Poison)"
        //$Sprite "FSZSA1"

        speed 8;

        Hideous_ZombieScientist.mindamage 2;
        Hideous_ZombieScientist.maxdamage 5;

        SeeSound "";
        ActiveSound "";
        MeleeSound "weapons/chunksplat";

        tag "$TAG_ZOMBIESCIENTIST_SYRINGE";
        HitObituary "$OB_ZOMBIESCIENTIST_SYRINGE_HIT";
        Obituary "$OB_ZOMBIESCIENTIST_SYRINGE";

        DropItem "PortableStimpack", 1;
    }

    states {
        spawn:
            FSZS A 0;
            goto spawn2;
        melee:
            #### BE 3 A_FaceTarget();
            #### F 5 A_WeaponAttack(1, "piercing");
            #### FE 4;
            loop;
        InjectedWait:
            #### FEED 4;
            #### D 0 A_UnBoopWithDrugs();
            goto see;
    }
}

class Dead_ZombieLadySyringe : Hideous_ZombieLadySyringe {
    override void postbeginplay() {
        super.postbeginplay();
        A_Die("spawndead");
    }
}
